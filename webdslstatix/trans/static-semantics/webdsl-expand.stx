module static-semantics/webdsl-expand

imports
  static-semantics/entities/annotations

  static-semantics/types/built-ins

  static-semantics/webdsl
  static-semantics/webdsl-actions
  static-semantics/webdsl-types
  static-semantics/webdsl-ui

rules  // unimplemented

  defOk(_, ExpandTemplateCall(_, _)) :- try { false } | warning $[This definition is not yet implemented].
  defOk(_, ExpandTemplateDefinition(_, _, _)) :- try { false } | warning $[This definition is not yet implemented].

  stmtOk(_, _, StatementExpandLocal(_, _, _), _) :- try { false } | warning $[This statement is not yet implemented].

  typeOfExp(_, exp@ExpExpandLocal(_, _, _)) = UNTYPED() :- try { false } | warning $[Typing of expression [exp] is not yet implemented].

rules

  templateElementOk(s, _, s_pha, DeriveNoFromNoFor(p, body)) :-
    deriveStringOk(p),
    deriveBodyOk(s, s_pha, body),
    false | error $[Cannot derive for non-entity types, use syntax "derive <pagetype> from <var> { ... }"].

  templateElementOk(s, _, s_pha, Derive(p, exp, props, body)) :- { t }
    deriveStringOk(p),
    t == typeOfExp(s, exp),
    typeCompatible(t, entity(s)) | error $[Cannot derive for non-entity types] @exp,
    derivePropertiesOk(s, t, p, props),
    deriveBodyOk(s, s_pha, body).

  deriveStringOk : string
  deriveStringOk(_) :- false | error $[Can only derive "viewPage", "createPage" or "editPage"].
  deriveStringOk("viewPage").
  deriveStringOk("viewRows").
  deriveStringOk("createPage").
  deriveStringOk("editPage").
  deriveStringOk("editRows").
  deriveStringOk("editRowsInline").

  derivePropertiesOk maps derivePropertyOk(*, *, *, list(*))
  derivePropertyOk : scope * TYPE * string * DeriveProperty
  derivePropertyOk(_, _, _, _) :- try { false } | warning $[This element is not yet supported].
  derivePropertyOk(s, t_ent, _, DeriveDefault(prop)) :- { entityName t }
    t_ent == ENTITY(entityName, _),
    t == typeOfProperty(s, t_ent, prop) | error $[Entity [entityName] has no property [prop]], // correct error message for tests
    isMutableProperty(t_ent, prop) | error $[Property [prop] is not mutable].

  derivePropertyOk(s, t_ent, "viewPage", DeriveDefault(prop)) :- { entityName t }
    t_ent == ENTITY(entityName, _),
    t == typeOfProperty(s, t_ent, prop) | error $[Entity [entityName] has no property [prop]]. // correct error message for tests

  derivePropertyOk(s, t_ent, "viewRows", DeriveDefault(prop)) :- { entityName t }
    t_ent == ENTITY(entityName, _),
    t == typeOfProperty(s, t_ent, prop) | error $[Entity [entityName] has no property [prop]]. // correct error message for tests

  deriveBodyOk : scope * scope * DeriveBody
  deriveBodyOk(s, s_pha, DeriveBody(elements)) :- deriveBodyElementsOk(s, s_pha, elements).

  deriveBodyElementsOk maps deriveBodyElementOk(*, *, list(*))
  deriveBodyElementOk : scope * scope * DeriveBodyElement
  deriveBodyElementOk(_, _, _) :- try { false } | warning $[This element is not yet supported].

rules

  defOk(s, DeriveCrud(ent)) :- { t }
    t == typeOfSort(s, SimpleSort(ent)) | error $[The entity [ent] does not exist],
    typeCompatible(t, entity(s)) | error $[The entity [ent] does not exist].
