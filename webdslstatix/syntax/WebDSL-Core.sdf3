module WebDSL-Core

imports
  WebDSL-Action
  WebDSL-DataModel
  WebDSL-Lexical

context-free sorts

  FormalArg OptFormalArgs FunctionSortParams FunctionSortParam FunctionSortReturn OptCallArgs Sort

context-free syntax %% Types

  Sort.SimpleSort = <<SimpleSortLex>>
  Sort.GenericSort = <<GenericSortLex> \< <{Sort ","}+> \>>
  Sort.ListSort = <[ <Sort> ]>
  Sort.SetSort = <{ <Sort> }>
  Sort.RefSort = <ref <Sort>>
  Sort.RefSortAlt = <Ref \< <Sort> \>>
  Sort.FunctionSort = <function <FunctionSortParams> <FunctionSortReturn>>

lexical syntax

  SimpleSortLex = "function" {reject}

context-free syntax

  %% function type; optionally specify (named) args and return type
  FunctionSortParams.FunctionSortParams = <( <{FunctionSortParam ","}*> )>
  FunctionSortParams.FunctionSortParamsNone = <>
  FunctionSortParam.UnnamedArg = Sort
  FunctionSortParam.NamedArg = <<VarId> : <Sort>>
  FunctionSortReturn.FunctionSortReturn = <: <Sort>>
  FunctionSortReturn.FunctionSortReturnNone = <>

  FormalArg.Arg = <<VarId> : <Sort>>
  OptFormalArgs.FormalArgs = <( <{FormalArg ","}*> )>
  OptFormalArgs.OptFormalArgsNone = <>

  OptCallArgs.CallArgs = <( <{Exp ","}*> )>
  OptCallArgs.OptCallArgsNone = <>

context-free sorts

  Unit Section Definition EnumValue

context-free syntax

  Section.Section = <
    section <SectionName>
      <Definition*>
  >

context-free restrictions

  // TO-DO: this can still go wrong in case two or more spaces are given before the newline
  "section" -/- [\ ] . [\r\n]
  "section" -/- [\r\n]

context-free syntax

  Unit.ApplicationDefs = <
    application <QId>
      <Definition+>
    <Section*>
  >

  Unit.Application = <
    application <QId>
    <Section*>
  >

  Unit.ModuleDefs = <
    module <ModuleName>
      <Definition+>
    <Section*>
  >

  Unit.Module = <
    module <ModuleName>
    <Section*>
  >

  Unit.BuiltInDefs = <
    built-in
      <Definition+>
    <Section*>
  >

  Unit.BuiltIn = <
    built-in
    <Section*>
  >

  Definition.Imports = <
    imports <ImportName>
  >

  Definition.ImportsBuiltIn = <
    imports built-in
  >

lexical syntax

  ModuleName = "built-in" {reject}

context-free syntax

  Definition.Description = <
    description { <Word*> }
  >
  Definition.Note = <
    note { <Word*> }
  >

  Definition.Enum = <
    enum <Id> {
      <{EnumValue ","}*>
    }
  >
  EnumValue.EnumValue = <<Id> ( <String> )>

context-free sorts

  Action Statement

context-free syntax

  Definition = Action
